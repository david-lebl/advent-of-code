advent.day1.decode: [Int] -> Int
advent.day1.decode input =
    let
        loop : Int -> Int -> [Int] -> Int
        loop dial zeros = cases
          [] -> zeros
          head +: tail -> 
            let
                moved = (dial + head) % +100
                if moved == +0 then loop moved (zeros + +1) tail
                else loop moved zeros tail
        loop +50 +0 input

advent.day1.program: Text -> {Exception} Int
advent.day1.program input = 
    unwrap: Text -> Optional a -> {Exception} a
    unwrap msg = cases
      Some value -> value
      None -> bug msg
    parse: Text -> Int
    parse line = 
      match Text.uncons line with
        Some(?R, xs) -> Int.fromText(xs) |> unwrap ("invalid number" ++ xs)
        Some(?L, xs) -> Int.fromText(xs) |> unwrap ("invalid number" ++ xs) |> Int.negate
        _ -> bug ("Failed to parse: " ++ line)
    let
        lines = splitOnNewline input
        moves = List.map parse lines
        decode moves

advent.day1.example: '{IO, Exception} ()
advent.day1.example _ = 
    result = program """
    L68
    L30
    R48
    L5
    R60
    L55
    L1
    L99
    R14
    L82
    """
    printLine ("Password is: " ++ (Int.toText result))

advent.day1.fullData: '{IO, Exception} ()
advent.day1.fullData _ = 
    inputData = readFileUtf8 (FilePath "../inputs/day1_input.txt")
    result = program inputData
    printLine ("Password is: " ++ (Int.toText result))

